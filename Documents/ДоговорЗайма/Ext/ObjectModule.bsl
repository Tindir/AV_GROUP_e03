#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения, Отказ);
	ПодготовитьТаблицыДляДвижений(Ссылка, ДополнительныеСвойства, РежимПроведения, Отказ);
	СформироватьДвиженияВРегистры(ДополнительныеСвойства, Движения, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения, Отказ) Экспорт 
	
	ДополнительныеСвойства.Вставить("ВалютаРегУчета", Константы.ВалютаРегламентированногоУчета.Получить());
	
КонецПроцедуры

Процедура ПодготовитьТаблицыДляДвижений(Ссылка, ДополнительныеСвойства, РежимПроведения, Отказ) Экспорт
	
	ДополнительныеСвойства.Вставить("ТаблицыДляДвижений", новый Структура());
	
	ПодготовитьТаблицыДляДвижений_ПараметрыДоговораЗайма(Ссылка, ДополнительныеСвойства, РежимПроведения, Отказ);	
	ПодготовитьТаблицыДляДвижений_ПараметрыДоговораЗаймаАгент(Ссылка, ДополнительныеСвойства, РежимПроведения, Отказ);	
	
КонецПроцедуры

Процедура ПодготовитьТаблицыДляДвижений_ПараметрыДоговораЗайма(Ссылка, ДополнительныеСвойства, РежимПроведения, Отказ) Экспорт
	
	ДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ПараметрыДоговораЗайма", Новый ТаблицаЗначений);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументСсылка", Ссылка);
	Запрос.Текст = "ВЫБРАТЬ
	               |	ДоговорЗайма.ДатаНачала КАК Период,
	               |	ДоговорЗайма.Организация КАК Организация,
	               |	ДоговорЗайма.Контрагент КАК Контрагент,
	               |	ДоговорЗайма.Ссылка КАК ДоговорЗайма,
	               |	ДоговорЗайма.ДатаНачала КАК ДатаНачала,
	               |	ДоговорЗайма.ДатаОкончания КАК ДатаОкончания,
	               |	ДоговорЗайма.Валюта КАК Валюта,
	               |	ДоговорЗайма.СчетУчета КАК СчетУчета,
	               |	ДоговорЗайма.СчетУчетаПроцентов КАК СчетУчетаПроцентов,
	               |	ДоговорЗайма.Процент КАК Процент,
	               |	ДоговорЗайма.Сумма КАК СуммаЗайма,
	               |	ЛОЖЬ КАК Состояние
	               |ИЗ
	               |	Документ.ДоговорЗайма КАК ДоговорЗайма
	               |ГДЕ
	               |	ДоговорЗайма.Ссылка = &ДокументСсылка";
	//
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выгрузить();
	
	ДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ПараметрыДоговораЗайма", Выборка);
	
КонецПроцедуры

Процедура ПодготовитьТаблицыДляДвижений_ПараметрыДоговораЗаймаАгент(Ссылка, ДополнительныеСвойства, РежимПроведения, Отказ) Экспорт
	
	ДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ПараметрыДоговораЗаймаАгент", Новый ТаблицаЗначений);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументСсылка", Ссылка);
	Запрос.Текст = "ВЫБРАТЬ ДоговорЗайма.Дата КАК Период,
	               |	ДоговорЗайма.Организация КАК Организация,
	               |	ДоговорЗайма.Агент КАК Контрагент,
	               |	ДоговорЗайма.Ссылка КАК ДоговорЗайма,
	               |	ДоговорЗайма.ДатаНачала КАК ДатаНачала,
	               |	ДоговорЗайма.ДатаОкончания КАК ДатаОкончания,
	               |	ДоговорЗайма.Валюта КАК Валюта,
	               |	ДоговорЗайма.ПроцентАгента КАК Процент,
	               |	ЛОЖЬ КАК Состояние
	               |ИЗ
	               |	Документ.ДоговорЗайма КАК ДоговорЗайма
	               |ГДЕ
	               |	ДоговорЗайма.Ссылка = &ДокументСсылка";
	//
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выгрузить();
	
	ДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ПараметрыДоговораЗаймаАгент", Выборка);
	
КонецПроцедуры

Процедура СформироватьДвиженияВРегистры(ДополнительныеСвойства, Движения, Отказ) Экспорт
	
	ТаблицаДвижений = ДополнительныеСвойства.ТаблицыДляДвижений.ПараметрыДоговораЗайма;
	Если ТаблицаДвижений.Количество() > 0 Тогда
		Движения.ПараметрыДоговораЗайма.Записывать = Истина;
		Движения.ПараметрыДоговораЗайма.Загрузить(ТаблицаДвижений);
	КонецЕсли;
	
	ТаблицаДвижений = ДополнительныеСвойства.ТаблицыДляДвижений.ПараметрыДоговораЗаймаАгент;
	Если ТаблицаДвижений.Количество() > 0 Тогда
		Движения.ПараметрыДоговораЗаймаАгент.Записывать = Истина;
		Движения.ПараметрыДоговораЗаймаАгент.Загрузить(ТаблицаДвижений);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли